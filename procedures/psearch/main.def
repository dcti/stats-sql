-- $Id: main.def,v 1.7 2000/06/09 09:43:25 decibel Exp $

\set procedure=p_psearch

-- a blank 'use' aborts the next transaction, so do this in two steps
if ( "${1}" = "" )
begin
	print "Database not specified! Using 'stats' as default!"
	use stats
end
go
if ( "${1}" != "" )
begin
	print "Using database '%1!'", ${1}
	use ${1}
end
go

if (object_id("$procedure") is not NULL)
begin
	revoke exec on $procedure to public
	print "Dropping procedure ${procedure}"
	drop procedure $procedure
end
go

print "Creating procedure ${procedure}"
go

create procedure $procedure
(	@project		varchar(10) = null,	/* Project to look at */
	@searchtext		varchar(255) = null,	/* Text to search for */
	@maxrows		int = 50,		/* Max rows to return */
	@escapewildcards	bit = 1,		/* 1: escape wildcard characters in @searchtext */
	@project_id		tinyint = 0		/* PROJECT_ID */
)
as
begin
	set nocount on
	set rowcount 0

	declare @rows		int,
		@pattern	varchar(255),	/* Search pattern to use */
		@procname	varchar(30)	/* Name of the procedure to run */

	/* Error checking */

	/* Make sure all required parameters were specified */
	if ( (@project is null) or (@searchtext is null) )
	begin
		raiserror 99999 "Not enough parameters"
		print "usage:"
		print "	$procedure project, searchtext, maxrows, escapewildcards, project_id"
		print "where:"
		print "	project is project to look at"
		print " searchtext is the email to search for"
		print " maxrows is the maximum number of rows to return"
		print " escapewildcards is set to 1 if you want to escape wildcards in searchtext"
		print "		[default], or 0 if you do not want to escape them"
		print "	project_id is the project ID to search. Valid only when project='new'"
		return -5
	end

	/* Dont allow searches on less than three characters */
	if datalength(@searchtext) < 3
	begin
		raiserror 99999 "Search text is too short"
		print "searchtext must be at least three characters"
		return -5
	end

	/* Build the procedure name */
	select @procname = "$procedure" + "_" + @project

	/* See if the silly procedure exists */
	if (object_id(@procname) is NULL)
	begin
		raiserror 99999 "Procedure '%1!' does not exist!", @procname
		return -1
	end


	create table #psearch
	(
		id	int
	)

	select	@rows = 0

	-- See if we should escape the wildcards
	if @escapewildcards = 1
	begin
		exec p_escape_wildcards @searchtext, @pattern output
	end
	else
	begin
		select @pattern = @searchtext
	end

	-- Do the search
	select @pattern = @pattern + '%'

	if charindex('@', @pattern) > 1
	begin
		insert #psearch (id)
			select id
			from STATS_Participant
			where email like @pattern

		select @rows = @@rowcount
	end

	-- If we didn't get any rows back (maybe because we didn't even search), search again
	if @rows = 0
	begin
		select @pattern = '%' + @pattern

		insert #psearch
			select id
			from STATS_Participant
			where email like @pattern

		select @rows = @@rowcount
	end

	-- Actually execute the procedure
	set rowcount @maxrows
	set nocount off
	exec @procname @project_id
	set nocount on
	set rowcount 0

	drop table #psearch

        return
end
go
grant exec on $procedure to public
go
